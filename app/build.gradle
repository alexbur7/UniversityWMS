plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

android {
    compileSdk versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId versions.applicationId
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility versions.javaVers
        targetCompatibility versions.javaVers
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_version"
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$activity_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.7.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // COMPOSE
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation 'androidx.compose.material3:material3:1.1.0-beta02'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"

    //Dagger 2
    implementation "com.google.dagger:dagger-android:$hilt_version"
    kapt "com.google.dagger:dagger-compiler:$hilt_version"

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    //Navigation
    implementation "androidx.navigation:navigation-compose:2.5.3"

    //Serialization
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    //Network
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    implementation project(':core')
    implementation project(':uikit')
}

private Integer generateVersionCode() {
    def minSDK = versions.minSdk * 1000000
    def major = versions.versionMajor * 10000
    def minor = versions.versionMinor * 100
    def patch = versions.versionPatch
    def versionCode = minSDK + major + minor + patch
    return versionCode
}

private String generateVersionName() {
    return "${versions.versionMajor}.${versions.versionMinor}.${versions.versionPatch}"
}